plugins {
    id 'java'
    //id 'io.quarkus'
    id 'com.google.protobuf' version '0.9.5'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-grpc'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

String protobufVersion = "3.25.5"
String grpcVersion = "1.69.1"
protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protobufVersion"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
    }
    quarkusGrpc {
      artifact = "io.quarkus:quarkus-grpc-protoc-plugin:$quarkusPlatformVersion:shaded@jar"
    }
  }

  generateProtoTasks {
    all().configureEach { task ->
      task.plugins {
        grpc { }
        quarkusGrpc { }
      }
    }
  }
}
